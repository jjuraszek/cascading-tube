apply plugin: 'scala'
apply plugin: 'maven'
apply plugin: 'idea'


group = 'org.clojars.jj'
version = '0.14'

configurations {
  deployerJars
}

repositories {
  mavenCentral()
  maven {
    url 'http://conjars.org/repo'
  }
  maven{
    url 'https://repository.cloudera.com/artifactory/cloudera-repos'
  }
}

dependencies {
  compile(
      'org.scala-lang:scala-library:2.10.2',
      'org.scala-lang:scala-reflect:2.10.2',
      'cascading:cascading-core:2.1.6',
      'cascading:cascading-local:2.1.6',
      'cascading:cascading-hadoop:2.1.6',
      'org.apache.hadoop:hadoop-client:2.0.0-cdh4.2.1'
  )
  deployerJars 'org.apache.ant:ant-jsch:1.7.1', 'jsch:jsch:0.1.29'
}

task deploy(dependsOn: ['install','sourcesJar','javadocJar']) << {
  ant.taskdef(name: 'scp', classname: 'org.apache.tools.ant.taskdefs.optional.ssh.Scp',
      classpath: configurations.deployerJars.asPath)
  ant.scp(todir: 'repo@conjars.org:', keyfile: '${user.home}/.ssh/id_dsa', verbose: 'true', passphrase: '') {
    fileset(dir: 'build') {
      include(name: 'libs/*.jar')
      include(name: 'poms/*.xml')
    }
  }
}

tasks.withType(ScalaCompile) {
  scalaCompileOptions.additionalParameters = ['-target:jvm-1.6']
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

idea {
  module {
    downloadSources = true
  }
}
